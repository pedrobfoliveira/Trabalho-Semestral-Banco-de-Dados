-- Regra 1: Ponto de Reabastecimento
CREATE OR REPLACE TRIGGER TR_PRODUTO_LOJA_REABASTECIMENTO
AFTER UPDATE OF QTD_ESTOQUE ON PRODUTO_LOJA
FOR EACH ROW
DECLARE
  V_ESTOQUE_MINIMO NUMBER := 20;
BEGIN
  IF :NEW.QTD_ESTOQUE < V_ESTOQUE_MINIMO
     AND :OLD.QTD_ESTOQUE >= V_ESTOQUE_MINIMO THEN

    INSERT INTO ALERTAS_REABASTECIMENTO (ID_ALERTA, ID_PRODUTO_LOJA, ESTOQUE_NO_ALERTA)
    VALUES (SQ_ALERTAS_REABASTECIMENTO.NEXTVAL, :NEW.ID_PRODUTO_LOJA, :NEW.QTD_ESTOQUE);
  END IF;
END;
/

-- Regra 2a: Validar Estoque (impede compra sem estoque)
CREATE OR REPLACE TRIGGER TR_CHECK_ESTOQUE_VENDA
BEFORE INSERT ON COMPRA_VENDA_PRODUTO_LOJA
FOR EACH ROW
DECLARE
  V_ESTOQUE_ATUAL NUMBER;
BEGIN
  SELECT QTD_ESTOQUE
    INTO V_ESTOQUE_ATUAL
    FROM PRODUTO_LOJA
   WHERE ID_PRODUTO_LOJA = :NEW.ID_PRODUTO_LOJA;

  IF :NEW.QUANTIDADE > V_ESTOQUE_ATUAL THEN
    RAISE_APPLICATION_ERROR(
      -20222,
      'Estoque insuficiente para o produto. Disponível: ' || V_ESTOQUE_ATUAL ||
      ', Pedido: ' || :NEW.QUANTIDADE
    );
  END IF;

EXCEPTION
  WHEN NO_DATA_FOUND THEN
    RAISE_APPLICATION_ERROR(
      -20002,
      'Produto não encontrado na tabela PRODUTO_LOJA para o ID ' || :NEW.ID_PRODUTO_LOJA
    );
END;
/

-- Regra 2b: Modificar Estoque (abate estoque após venda)
CREATE OR REPLACE TRIGGER TR_UPDATE_ESTOQUE_VENDA
AFTER INSERT ON COMPRA_VENDA_PRODUTO_LOJA
FOR EACH ROW
BEGIN
  UPDATE PRODUTO_LOJA
     SET QTD_ESTOQUE = QTD_ESTOQUE - :NEW.QUANTIDADE
   WHERE ID_PRODUTO_LOJA = :NEW.ID_PRODUTO_LOJA;
END;
/

-- Regra 3: Limite de redução de preço
CREATE OR REPLACE TRIGGER TR_LIMITE_REDUCAO_PRECO
BEFORE UPDATE OF PRECO_VENDA_UNITARIO ON PRODUTO_LOJA
FOR EACH ROW
DECLARE
  V_PERCENTUAL_MAX_REDUCAO NUMBER := 0.10;
  V_PRECO_MINIMO_PERMITIDO NUMBER;
BEGIN
  IF :NEW.PRECO_VENDA_UNITARIO < :OLD.PRECO_VENDA_UNITARIO THEN
    V_PRECO_MINIMO_PERMITIDO := :OLD.PRECO_VENDA_UNITARIO * (1 - V_PERCENTUAL_MAX_REDUCAO);

    IF :NEW.PRECO_VENDA_UNITARIO < V_PRECO_MINIMO_PERMITIDO THEN
      RAISE_APPLICATION_ERROR(
        -20003,
        'Redução de preço excede o limite de ' ||
        (V_PERCENTUAL_MAX_REDUCAO * 100) || '%. Preço mínimo: R$' ||
        TO_CHAR(V_PRECO_MINIMO_PERMITIDO, 'FM999G999G990D00')
      );
    END IF;
  END IF;
END;
/


-- Regra 5: Alocação de Funcionário para Aferição
CREATE OR REPLACE TRIGGER TR_VALIDAR_AFERICAO_BICO
BEFORE INSERT ON AFERICOES_BICO
FOR EACH ROW
DECLARE
  V_CARGO_ID        NUMBER;
  V_CARGO_PERMITIDO NUMBER := 3; -- ID do cargo operador de aferição
BEGIN
  SELECT ID_CARGO
    INTO V_CARGO_ID
    FROM FUNCIONARIO_CARGO
   WHERE ID_FUNCIONARIO = :NEW.ID_FUNCIONARIO
     AND DATA_SAIDA IS NULL;

  IF V_CARGO_ID <> V_CARGO_PERMITIDO THEN
    RAISE_APPLICATION_ERROR(
      -20005,
      'Funcionário não autorizado para aferição de bico. Apenas operadores de aferição são permitidos.'
    );
  END IF;

EXCEPTION
  WHEN NO_DATA_FOUND THEN
    RAISE_APPLICATION_ERROR(
      -20006,
      'Funcionário não possui cargo ativo para realizar aferição.'
    );
END;
/